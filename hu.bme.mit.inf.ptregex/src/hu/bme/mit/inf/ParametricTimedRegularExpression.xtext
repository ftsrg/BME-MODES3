grammar hu.bme.mit.inf.ParametricTimedRegularExpression with org.eclipse.xtext.common.Terminals

generate parametricTimedRegularExpression "http://www.bme.hu/mit/inf/ParametricTimedRegularExpression"

RegexModel:
	alphabet=Alphabet?
	(declarations+=ExpressionDeclaration)+;

Alphabet:
	{Alphabet} 'alphabet' '=' '{' (functors+=Functor (',' functors+=Functor)*)? '}';

Functor:
	name=ID ('/' arity=INT)?;

ExpressionDeclaration:
	'expression' name=ID ('[' vars+=Var (',' vars+=Var)* ']')? '=' body=Expression;

Expression:
	Choice; // ezt modositottam choicerol
Choice returns Expression:
	And ({Choice.elements+=current} ('|' elements+=And)+)?;

And returns Expression:
	Sequence ({And.elements+=current} ('&' elements+=Sequence)+)?;

Sequence returns Expression:
	MultExpression ({Sequence.elements+=current} elements+=MultExpression+)?;

MultExpression returns Expression:
	PonNegExpression ({Star.body=current} '*' | {Plus.body=current} '+' | {Cardinality.body=current} '{' n=INT '}')?;

PonNegExpression returns Expression:
	NegExpression | ParenExpression;

NegExpression returns Expression:
	"!" body=ParenExpression;

ParenExpression returns Expression:
	'(' Expression ')' | Any | Inverse | TimedExpression | Event;

Any:
	{Any} 'S';

Inverse:
	'(' 'S' '\\' excludes+=Event (',' excludes+=Event)* ')';

TimedExpression:
	'<' body=Expression '>' '[' timeout=INT ']';

Event:
	functor=[Functor] ('[' parameters+=Parameter (',' parameters+=Parameter)* ']')?;

Parameter:
	VarParameter | SingletonParameter | FixParameter;

FixParameter:
	FixIntParameter | FixStringParameter;

FixIntParameter:
	body=INT;

FixStringParameter:
	body=STRING;

VarParameter:
	var=[Var];

SingletonParameter:
	{SingletonParameter} '_';

Var:
	name=ID;

terminal ID:
	'^'? ('a'..'z' | 'A'..'Z') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;
